package homeioexample

declare AlarmArmed
    @role ( event )
end

declare AlarmRaised
    @role ( event )
end


rule "Alarm armed"
    when
        not( AlarmArmed() )
        ItemStateChangedEvent( name == "E_Alarm_Key_Pad_Armed", newState == ARMED)
    then
        insert(new AlarmArmed());
end

rule "Alarm disarmed"
    when
        $armed : AlarmArmed()
        ItemStateChangedEvent( name == "E_Alarm_Key_Pad_Armed", newState == DISARMED)
    then
        retract($armed);
end


rule "Alarm for motion or door open"
    when
        $armed : AlarmArmed()
        not ( AlarmRaised() )
        
        ItemStateChangedEvent( this after[10s] $armed && ( 
        ( name matches ".*Motion_Detector" && newState == MOTION ) || 
        ( name matches ".*Door_Detector.*" && newState == DOOR_OPENED ) ) )
    then
        insert(new AlarmRaised());
end


rule "Alarm for light"
    when
        $armed : AlarmArmed()
        not ( AlarmRaised() )
        $firstBrigthness : ItemStateChangedHistory(this after[10s] $armed, name matches ".*Brightness_Sensor", newState == BRIGHTNESS)
        
        $secondBrigthness : ItemStateChangedEvent(this after[5s, 1m] $firstBrigthness, name matches ".*Brightness_Sensor", newState == BRIGHTNESS)
    then
        insert(new AlarmRaised());
end


rule "Alarm raised"
    when
        AlarmRaised()
    then
        openhab.postCommand("E_Siren", ON);
        openhab.postCommand("O_Siren", ON);
end


rule "Alarm stop"
    when
        not( AlarmArmed() )
        $alarmRaised : AlarmRaised()
    then
        retract($alarmRaised);
        openhab.postCommand("E_Siren", OFF);
        openhab.postCommand("O_Siren", OFF);
end
